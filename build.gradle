plugins {
    id 'java'
}

group 'mhahnFr'
version '3.2.3'

configurations {
    jutils
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}

tasks.register('deploy', Exec) {
    dependsOn(jar)

    commandLine 'jpackage', '--input',        'build/libs',
                            '--name',         project.name,
                            '--main-jar',     project.name + '-' + version + '.jar',
                            '--copyright',    'Copyright Â© 2014 - 2023 mhahnFr',
                            '--app-version',  version,
                            '--description',  'Radio-Text part of iSongs',
                            '--vendor',       project.group,
                            '--about-url',    'https://github.com/mhahnFr/iSongs-RadioText',
                            '--license-file', 'LICENSE',
                            '--icon',         'resources/icon.icns'
}

jar {
    dependsOn configurations.jutils
    manifest {
        attributes 'Main-Class': 'mhahnFr.iSongs.iSongs'
    }
    from {
        configurations.jutils.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    implementation project(':JUtilities')
    jutils project(':JUtilities')
}