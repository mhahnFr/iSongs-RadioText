/*
 * iSongs-RadioText - Radio-text part of iSongs.
 *
 * Copyright (C) 2024  mhahnFr
 *
 * This file is part of the iSongs-RadioText.
 *
 * iSongs-RadioText is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * iSongs-RadioText is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * iSongs-RadioText, see the file LICENSE.  If not, see <https://www.gnu.org/licenses/>.
 */

import java.nio.file.Files
import java.nio.file.StandardCopyOption


plugins {
    id 'java'
}

group 'mhahnFr'
version '3.3.1'

def appleScripts = new File('scripts').listFiles()

repositories {
    mavenCentral()
}

configurations {
    jutils
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs 'scripts'
            include '**/*.applescript'
        }
    }
}

tasks.register('deploy', Exec) {
    dependsOn(jar)

    for (final var file : appleScripts) {
        if (!file.name.endsWith('.scpt')) continue

        Files.copy(file.toPath(), new File('build/libs/' + file.name).toPath(), StandardCopyOption.REPLACE_EXISTING)
    }

    configurations.runtimeClasspath.each {
        Files.copy(it.toPath(), new File('build/libs/' + it.name).toPath(), StandardCopyOption.REPLACE_EXISTING)
    }

    commandLine 'jpackage', '--input',        'build/libs',
                            '--name',         project.name,
                            '--main-jar',     project.name + '-' + version + '.jar',
                            '--copyright',    'Copyright Â© 2014 - 2024 mhahnFr',
                            '--app-version',  version,
                            '--description',  'Radio-Text part of iSongs',
                            '--vendor',       project.group,
                            '--about-url',    'https://github.com/mhahnFr/iSongs-RadioText',
                            '--icon',         'resources/icon.icns'
}

tasks.register('maybeAppleScript', Exec) {
    if (System.getProperty('os.name').toLowerCase().contains('mac')) {
        for (final var script : appleScripts) {
            if (!script.name.endsWith('.applescript')) continue

            commandLine 'osacompile', '-xo', script.getParent() + File.separator + script.name.substring(0, script.name.lastIndexOf('.')) + '.scpt', script
        }
    }
}

dependencies {
    implementation 'com.formdev:flatlaf:3.4.1'
    implementation project(':JUtilities')
    jutils project(':JUtilities')
}

jar {
    dependsOn configurations.jutils
    dependsOn maybeAppleScript
    manifest {
        attributes 'Main-Class': 'mhahnFr.iSongs.iSongs', 'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' ')
    }
}